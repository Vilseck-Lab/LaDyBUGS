* Generate CHARMM patch files for ne-GSLD
*

set fnex = 5.5
set builddir = prep
set box = 83.0
set temp = 298.15

!! Read in toppar stream file
bomblev -2
stream @builddir/toppar.str
bomblev 0
read rtf append card name @builddir/core.rtf
read param flex append card name @builddir/full_ligand.prm

!! Reads coordinates and pdb file for the protein
read sequ pdb name @{builddir}/cmet_pro.pdb
generate PROA first NTER last CTER setup warn
read coor pdb resid name @{builddir}/cmet_pro.pdb

ic generate
ic param
ic build
hbuild sele hydrogen end
auto angle dihe

!! Load ligand + patches, affects the connectivity
stream @builddir/lig_specific.str

!! Load solvent
read sequ pdb name @{builddir}/fitted-solv_10.0.pdb
generate SOLV first none last none setup noangl nodihe
read coor pdb resid name @{builddir}/fitted-solv_10.0.pdb

read sequ pdb name @{builddir}/cla.pdb
generate CLA first none last none setup noangl nodihe
read coor pdb resid name @{builddir}/cla.pdb

read sequ pdb name @{builddir}/sod.pdb
generate SOD first none last none setup noangl nodihe
read coor pdb resid name @{builddir}/sod.pdb

print coor sele .not. init end

bomblev 0

!! For OpenMM, translate the box to the origin
calc box2 = @box / 2
coor stat
coor translate xdir @box2 ydir @box2 zdir @box2 select all end

!! Write Initial System to Disk
write psf card name patch.psf
* patch psf file
*
write coor pdb card name patch.pdb
* patch pdb file
*
write coor card name patch.crd
* patch crd file
*

stop

